AC_INIT(src/gnocam.c)
AC_PREREQ(2.12)

AM_INIT_AUTOMAKE(GnoCam, 0.10)
AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE
AM_ACLOCAL_INCLUDE(macros)
AM_PROG_XML_I18N_TOOLS

dnl ******************
dnl Initialize libtool
dnl ******************
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS

dnl The following is a hack to get GnoCam to build on gettext-0.10.35 
dnl systems - BUILD_INCLUDED_LIBINTL is set only in gettext-0.10.37 which is
dnl the gettext version provided with gphoto2.
BUILD_INCLUDED_LIBINTL=no
AC_SUBST(BUILD_INCLUDED_LIBINTL)

GNOME_INIT
AM_PATH_GTK(1.2.7)
GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS
GNOME_XML_CHECK

dnl Add the languages which your application supports here.
ALL_LINGUAS=""
AM_GNOME_GETTEXT
gnomelocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(gnomelocaledir)

dnl **********
dnl gnome-libs
dnl **********
AC_MSG_CHECKING(for gnome-libs >= 1.0.55)
if gnome-config --version > /dev/null 2>&1; then
    vers=`gnome-config --version | awk 'BEGIN { FS = "."; } { printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
    if test "$vers" -ge 55; then
	AC_MSG_RESULT(found)
    else
	AC_MSG_ERROR([
*** You need at least gnome-libs 1.0.55 for this version of ${PACKAGE}.])
    fi
else
    AC_MSG_ERROR([
*** Did not find gnome-libs installed.])
fi

dnl ******
dnl bonobo
dnl ******
AC_MSG_CHECKING(for bonobo>=0.37)
if gnome-config --libs bonobo > /dev/null 2>&1; then
    vers=`gnome-config --modversion bonobo | sed -e 's/^bonobo-//' | awk -F. '{ printf "%d", $1 * 1000 + $2;}'`
        if test "$vers" -ge 37; then
            AC_MSG_RESULT(found)
        else
            AC_MSG_ERROR([
*** You need at least bonobo-0.37 for this version of ${PACKAGE}. 
*** Get the latest version from http://www.gnome.org. Mirrors can
*** be found at http://www.gnome.org/mirrors.])
        fi
else
    AC_MSG_ERROR([
*** You need bonobo for this version of ${PACKAGE}. Get the latest 
*** version from http://www.gnome.org. Mirrors can be found at
*** http://www.gnome.org/mirrors.]) 
fi

dnl ***
dnl gal
dnl ***
AC_MSG_CHECKING(for GAL >= 0.7.0.0)
if gnome-config --libs gal > /dev/null 2>&1; then
    vers=`gnome-config --modversion gal | sed -e "s/gal-//" -e 's/cvs$//' -e 's/pre$//' | \
        awk 'BEGIN { FS = "."; } { print $1 * 1000000 + $2 * 10000 + $3 * 100 + $4;}'`
    if test "$vers" -ge 70000; then
        AC_MSG_RESULT(found)
    else
        AC_MSG_ERROR([
*** You need at least GAL 0.7.0.0 for this version of ${PACKAGE}.
*** Get the latest version from http://www.gnome.org. Mirrors can 
*** be found at http://www.gnome.org/mirrors.])
    fi
else
    AC_MSG_ERROR([
*** You need GAL for this version of ${PACKAGE}. Get the latest
*** version from http://www.gnome.org. Mirrors can be found at
*** http://www.gnome.org/mirrors.])
fi

dnl *********
dnl gnome-vfs
dnl *********
AC_MSG_CHECKING(for vfs)
if gnome-config --libs vfs > /dev/null 2>&1; then
    AC_MSG_RESULT(found)
    VFS_LIBS="`gnome-config --libs vfs`"
    VFS_CFLAGS="`gnome-config --cflags vfs`"
    AC_SUBST(VFS_LIBS)
    AC_SUBST(VFS_CFLAGS)
else
    AC_MSG_ERROR([
*** You need gnome-vfs for this version of ${PACKAGE}. Get the latest 
*** version from http://www.gnome.org. Mirrors can be found at 
*** http://www.gnome.org/mirrors.])
fi

dnl ******************************
dnl gnome-print
dnl ******************************
AC_MSG_CHECKING(for GnomePrint libraries >= 0.16)
if gnome-config --libs print > /dev/null 2>&1; then 
    vers=`gnome-config --modversion print | sed -e "s/gnome-print-//" | awk 'BEGIN { FS = "."; } { print $1 * 1000 + $2;}'`
    if test "$vers" -ge 16; then
        AC_MSG_RESULT(found)
    else
    	AC_MSG_ERROR([
*** You need at least gnome-print 0.16 for this version of ${PACKAGE}.
*** Get the latest version from http://www.gnome.org.
*** Mirrors can be found at http://www.gnome.org/mirrors.])
    fi
else
    AC_MSG_ERROR([
*** You need at least gnome-print 0.16 for this version of ${PACKAGE}.
*** Get the latest version from http://www.gnome.org.
*** Mirrors can be found at http://www.gnome.org/mirrors.])
fi

dnl ******************************
dnl Capplet
dnl ******************************
AC_MSG_CHECKING(for capplet libraries)
if gnome-config --libs capplet > /dev/null 2>&1; then
	AC_MSG_RESULT(found)
	CAPPLET_LIBS="`gnome-config --libs capplet`"
	CAPPLET_CFLAGS="`gnome-config --cflags capplet`"
	AC_SUBST(CAPPLET_LIBS)
	AC_SUBST(CAPPLET_CFLAGS)
else
	AC_MSG_ERROR([
*** You need the capplet library installed for this version of
*** ${PACKAGE}. Get a stable version of the GNOME control-center
*** from http://www.gnome.org.
*** Mirrors can be found at http://www.gnome.org/mirrors.])
fi

dnl ******************************
dnl GConf
dnl ******************************
AM_PATH_GCONF(0.12,,,gconf-gtk)

EXTRA_GNOME_CFLAGS="`gnome-config --cflags bonobo bonobox bonobox_print print gal gnome gnomeui oaf`"
EXTRA_GNOME_LIBS="`gnome-config --libs bonobo bonobox bonobox_print print gal gnome gnomeui oaf`"
AC_SUBST(EXTRA_GNOME_CFLAGS)
AC_SUBST(EXTRA_GNOME_LIBS)

dnl *********************************************
dnl Keep the code clean - make all warnings fatal
dnl *********************************************
CFLAGS="$CFLAGS -Wall -Wchar-subscripts -Wmissing-declarations -Wmissing-prototypes -Wnested-externs -Wpointer-arith -Werror"
AC_SUBST(CFLAGS)

dnl **********
dnl libgphoto2
dnl **********
AC_MSG_CHECKING(for libgphoto2)
if gphoto2-config --libs > /dev/null 2>&1; then
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR([
*** You need libgphoto2 for this version of ${PACKAGE}.
*** Get it from http://sourceforge.net/projects/gphoto/])
fi
GPHOTO_CFLAGS="`gphoto2-config --cflags` `gphoto2-port-config --cflags`"
GPHOTO_LIBS="`gphoto2-config --libs` `gphoto2-port-config --cflags`"
AC_SUBST(GPHOTO_CFLAGS)
AC_SUBST(GPHOTO_LIBS)

dnl *********
dnl Makefiles
dnl *********
AC_OUTPUT([
gnocam.spec
Makefile
art/Makefile
intl/Makefile
macros/Makefile
po/Makefile.in
idl/Makefile
gnocam/Makefile
capplet/Makefile
bonobo-moniker/Makefile
gnome-vfs-module/Makefile
tests/Makefile
])

dnl *********************
dnl Give some feedback...
dnl *********************

echo "

***
I didn't check for an image or movie viewer. If you 
would like to view images or movies with ${PACKAGE}, 
you would probably like to install for example eog
for viewing images.
***

Configuration

	Source code location: ${srcdir}
	Compiler:             ${CC}
"
