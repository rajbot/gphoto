dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/gallery.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gphoto, 0.4.4)
AM_MAINTAINER_MODE

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AM_PROG_LIBTOOL

dnl Completed by AM_INIT_AUTOMAKE
dnl AC_PROG_INSTALL
dnl AC_PROG_MAKE_SET

dnl Add the languages which your application supports here.
ALL_LINGUAS="da de hu it no ru sl sv uk"
AM_GNU_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi


LIBGPIO_MAJOR_VERSION=0
LIBGPIO_MINOR_VERSION=0
LIBGPIO_MICRO_VERSION=1
LIBGPIO_VERSION=$LIBGPIO_MAJOR_VERSION.$LIBGPIO_MINOR_VERSION.$LIBGPIO_MICRO_VERSION
LIBGPIO_VERSION_INFO=`expr $LIBGPIO_MAJOR_VERSION + $LIBGPIO_MINOR_VERSION`:$LIBGPIO_MICRO_VERSION:$LIBGPIO_MINOR_VERSION

AC_SUBST(LIBGPIO_MAJOR_VERSION)
AC_SUBST(LIBGPIO_MINOR_VERSION)
AC_SUBST(LIBGPIO_MICRO_VERSION)
AC_SUBST(LIBGPIO_VERSION)
AC_SUBST(LIBGPIO_VERSION_INFO)

dnl Checks for libraries.
dnl Check -ldl
AC_CHECK_LIB(dl, dlopen)
dnl Check for X
AC_PATH_X
dnl Check -ljpeg:
AC_CHECK_LIB(jpeg, jpeg_start_decompress)
dnl Check for glib
AM_PATH_GLIB(1.2.6)
dnl Check for gtk
AM_PATH_GTK(1.2.6)
dnl Check for gdk_imlib
AM_PATH_GDK_IMLIB(1.9.0)
CFLAGS="$CFLAGS -I`pwd`/libgpio"
dnl LDFLAGS="$LDFLAGS -L`pwd`/libgpio -lgpio0"  We do not have libgpio installed yet.

dnl Solaris needs posix4 library
changequote(, )dnl
if test -n "`echo $host_os | grep [sS]olaris`"; then
  LIBS="$LIBS -lposix4"
fi
changequote([, ])dnl

AC_SUBST(CFLAGS)
AC_SUBST(LIBS)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_CHECK_HEADERS(sys/param.h termios.h sgetty.h ttold.h ioctl-types.h \
	fcntl.h sgtty.h sys/ioctl.h sys/time.h termio.h unistd.h \
	endian.h byteswap.h asm/io.h getopt.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_INLINE
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl Check if TIOCM_RTS is included in one of several possible files
AC_TRY_COMPILE([#include <termios.h>], [int foo = TIOCM_RTS;], 	
			AC_DEFINE(HAVE_RTS_IOCTL))
AC_TRY_COMPILE([#include <termio.h>], [int foo = TIOCM_RTS;], 	
			AC_DEFINE(HAVE_RTS_IOCTL))
AC_TRY_COMPILE([#include <ioctl-types.h>], [int foo = TIOCM_RTS;], 	
			AC_DEFINE(HAVE_RTS_IOCTL))
AC_TRY_COMPILE([#include <sys/ioctl.h>], [int foo = TIOCM_RTS;], 	
			AC_DEFINE(HAVE_RTS_IOCTL))

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(setreuid mkdir mktime strcspn strdup strerror strtol select \
	getopt_long getopt)

AC_PATH_PROG(LIBUSB_CONFIG,libusb-config)
if test -n "${LIBUSB_CONFIG}"; then
  CFLAGS="$CFLAGS `$LIBUSB_CONFIG --cflags`"
  GPIO_CFLAGS="$GPIO_CFLAGS `$LIBUSB_CONFIG --cflags`"
  LIBS="$LIBS `$LIBUSB_CONFIG --libs`"
  GPIO_USB=1
else
  GPIO_USB=0
fi

AM_CONDITIONAL(GPIO_USB, test $GPIO_USB = "1")
AC_SUBST(GPIO_USB)

dnl Local setup
dnl Is there a better way of doing docs?
GPHOTO_EXPAND_DIR(DOCDIR, "$datadir/gphoto/doc")
AC_DEFINE_UNQUOTED(DOCDIR, "$DOCDIR")

dnl Or gallery themes?
GPHOTO_EXPAND_DIR(GALLERYDIR, "$datadir/gphoto/gallery")
AC_DEFINE_UNQUOTED(GALLERYDIR, "$GALLERYDIR")

driversdir='${pkglibdir}'
AC_SUBST(driversdir)

gallerydir='${pkgdatadir}/gallery'
AC_SUBST(gallerydir)

AC_OUTPUT(Makefile \
intl/Makefile \
po/Makefile.in \
libgpio/Makefile \
libgpio/gpio.h \
barbie/Makefile \
canon/Makefile \
casio/Makefile \
digita/Makefile \
dimera/Makefile \
directory/Makefile \
fuji/Makefile \
gallery/Makefile \
gallery/CSStheme/Makefile \
gallery/CSSjimmac/Makefile \
gallery/Default/Makefile \
gallery/RedNGray/Makefile \
gphoto.spec \
kodak/Makefile \
kodak_generic/Makefile \
konica/Makefile \
konica_qmxxx/Makefile \
man/Makefile \
minolta/Makefile \
mustek/Makefile \
nikon/Makefile \
philips/Makefile \
photopc/Makefile \
jd11/Makefile \
polaroid/Makefile \
ricoh/Makefile \
samsung/Makefile \
sony/Makefile \
sony/dscf55/Makefile \
src/Makefile \
src/icons/Makefile)
