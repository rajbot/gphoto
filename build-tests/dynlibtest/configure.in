dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.59)
AC_INIT([dynamic library load test], [0.4],
        [gphoto-devel@lists.sourceforge.net], [dynlibtest])
AC_CONFIG_SRCDIR([Makefile.am])
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_MACRO_DIR([m4m])
AM_INIT_AUTOMAKE([gnu 1.8 dist-bzip2])

GP_CHECK_SHELL_ENVIRONMENT
GP_CONFIG_MSG([Source code location],[${srcdir}])


AM_CPPFLAGS="$CPPFLAGS"
AM_LDFLAGS="$LDFLAGS"

AM_CPPFLAGS="$AM_CPPFLAGS -I\$(top_srcdir)"


dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
dnl AC_DISABLE_STATIC
dnl AC_DISABLE_SHARED
AC_LIBLTDL_INSTALLABLE([libltdl])
dnl AC_LIBLTDL_CONVENIENCE([libltdl])
AC_LIBTOOL_WIN32_DLL
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL

AC_SUBST([LTDLINCL])
AC_SUBST([LIBLTDL])

GP_CONFIG_MSG([Compiler],[${CC}])
AC_DEFINE_UNQUOTED(HAVE_CC,"$CC",[The C compiler we're using])
GP_CONFIG_MSG()


dnl Find out how to dynamically load libraries
GP_DYNAMIC_LIBRARIES


# module directory
AC_SUBST([moddir],["\$(libdir)/dynlibtest"])

AM_CPPFLAGS="$AM_CPPFLAGS -DMODULE_DIR=\\\"\$(moddir)\\\""
AM_CPPFLAGS="$AM_CPPFLAGS -I\$(top_srcdir)/lib"


dnl ---------------------------------------------------------------------------
dnl Turn on debugging and all warnings when using gcc
dnl ---------------------------------------------------------------------------
if test "$GCC" = "yes"; then
	AM_CPPFLAGS="$AM_CPPFLAGS -g -Wall -Wmissing-declarations -Wmissing-prototypes"
	AM_LDFLAGS="$AM_LDFLAGS -g -Wall"
fi

AC_SUBST([AM_CPPFLAGS])
AC_SUBST([AM_LDFLAGS])


if test -d "$srcdir/libltdl"; then
	AC_CONFIG_SUBDIRS([libltdl])
fi

# ---------------------------------------------------------------------------
# Create output files
# ---------------------------------------------------------------------------
AC_CONFIG_FILES([
Makefile
m4m/Makefile
bin/Makefile
lib/Makefile
mod/Makefile
])
AC_OUTPUT

GP_CONFIG_OUTPUT
