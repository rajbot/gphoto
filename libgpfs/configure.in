AC_PREREQ(2.50)
AC_INIT(libgpfs/gpfs.h)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libgpfs, 1.0)
AM_MAINTAINER_MODE

dnl ---------------------------------------------------------------------------
dnl Versioning:
dnl  - AGE:      Increment if any interfaces have been added; set to 0 if
dnl              any interfaces have been removed. Removal has precedence over
dnl              adding, so set to 0 if both happened.
dnl  - REVISION: Increment any time the source changes; set to 0 if you
dnl              incremet CURRENT.
dnl  - CURRENT:  Increment if the interface has additions, changes, removals.
dnl ---------------------------------------------------------------------------
LIBGPFS_AGE=0
LIBGPFS_REVISION=0
LIBGPFS_CURRENT=0
AC_SUBST(LIBGPFS_AGE)
AC_SUBST(LIBGPFS_REVISION)
AC_SUBST(LIBGPFS_CURRENT)
LIBGPFS_VERSION_INFO=`expr $LIBGPFS_CURRENT + $LIBGPFS_REVISION`:$LIBGPFS_AGE:$LIBGPFS_REVISION
AC_SUBST(LIBGPFS_VERSION_INFO)

dnl ---------------------------------------------------------------------------
dnl We need some programs
dnl ---------------------------------------------------------------------------
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LIBTOOL

dnl ---------------------------------------------------------------------------
dnl Interantionalization
dnl ---------------------------------------------------------------------------
ALL_LINGUAS=""
# EVIL HACKS
BUILD_INCLUDED_LIBINTL=no
AC_SUBST(BUILD_INCLUDED_LIBINTL)
USE_INCLUDED_LIBINTL=no
AC_SUBST(USE_INCLUDED_LIBINTL)
AC_MSG_CHECKING([for po/Makevars requiring evil hack])
GETTEXT_PACKAGE="${PACKAGE}-${LIBGPFS_CURRENT}"
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",[The gettext domain we're using])
AC_SUBST(GETTEXT_PACKAGE)
if test -f po/Makevars.template
then
        sed -e "s/^DOMAIN.*/DOMAIN = ${GETTEXT_PACKAGE}/" < po/Makevars.template > po/Makevars
	AC_MSG_RESULT([yes. done.])
else
	AC_MSG_RESULT([no])
fi
AM_GNU_GETTEXT([external])
dnl ---------------------------------------------------------------------------
dnl Build the test program using the konica driver?
dnl ---------------------------------------------------------------------------
try_konica=false
have_konica=false
konica_msg="no (default)"
AC_ARG_WITH(konica, [  --with-konica            Compile the konica test program],[
	if test x$withval = xyes; then
		try_konica=true
	fi])
if $try_konica; then
	AC_PATH_PROG(GP_PORT_CONFIG,gphoto2-port-config)
	if test -n "${GP_PORT_CONFIG}"; then
		have_konica=true
		konica_msg="yes"
		KONICA_CFLAGS=`$GP_PORT_CONFIG --cflags`
		KONICA_LIBS=`$GP_PORT_CONFIG --libs`
	fi
fi
AM_CONDITIONAL(HAVE_KONICA,$have_konica)
AC_SUBST(KONICA_LIBS)
AC_SUBST(KONICA_CFLAGS)

dnl ---------------------------------------------------------------------------
dnl Turn on debugging and all warnings when using gcc
dnl ---------------------------------------------------------------------------
if test "$ac_cv_prog_gcc" = "yes"; then
	CFLAGS="$CFLAGS -g -Wall -Wmissing-declarations -Wmissing-prototypes"
	LDFLAGS="$LDFLAGS -g -Wall"
fi

AC_OUTPUT([
Makefile
libgpfs/Makefile
libgpfs/libgpfs.pc
tests/Makefile
tests/konica/Makefile
])

echo "

Configuration (libgpfs):

	Source code location:      ${srcdir}
	Version:                   ${VERSION}
	Compiler:                  ${CC}

	Konica test program:       ${konica_msg}
"
