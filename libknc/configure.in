AC_PREREQ(2.50)
AC_INIT(libknc/knc.h)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libknc,1.0)
AM_MAINTAINER_MODE

dnl ---------------------------------------------------------------------------
dnl Version
dnl ---------------------------------------------------------------------------
LIBKNC_AGE=0
LIBKNC_REVISION=0
LIBKNC_CURRENT=0
AC_SUBST(LIBKNC_AGE)
AC_SUBST(LIBKNC_REVISION)
AC_SUBST(LIBKNC_CURRENT)
LIBKNC_VERSION_INFO="`expr $LIBKNC_CURRENT + $LIBKNC_REVISION`:$LIBKNC_AGE:$LIBKNC_REVISION"
AC_SUBST(LIBKNC_VERSION_INFO)

dnl ---------------------------------------------------------------------------
dnl We need some programs
dnl ---------------------------------------------------------------------------
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LIBTOOL
AC_PROG_INTLTOOL

dnl ---------------------------------------------------------------------------
dnl Interantionalization
dnl ---------------------------------------------------------------------------
ALL_LINGUAS=""
# EVIL HACKS
BUILD_INCLUDED_LIBINTL=no
AC_SUBST(BUILD_INCLUDED_LIBINTL)
USE_INCLUDED_LIBINTL=no
AC_SUBST(USE_INCLUDED_LIBINTL)
AC_MSG_CHECKING([for po/Makevars requiring evil hack])
GETTEXT_PACKAGE="${PACKAGE}-${LIBKNC_CURRENT}"
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",[The gettext domain we're using])
AC_SUBST(GETTEXT_PACKAGE)
if test -f po/Makevars.template
then
        sed -e "s/^DOMAIN.*/DOMAIN = ${GETTEXT_PACKAGE}/" < po/Makevars.template > po/Makevars
	AC_MSG_RESULT([yes. done.])
else
	AC_MSG_RESULT([no])
fi
AM_GNU_GETTEXT([external])

dnl ---------------------------------------------------------------------------
dnl Turn on debugging and all warnings when using gcc
dnl ---------------------------------------------------------------------------
if test "$ac_cv_prog_gcc" = "yes"; then
        CFLAGS="$CFLAGS -g -Wall -Wmissing-declarations -Wmissing-prototypes"
	LDFLAGS="$LDFLAGS -g -Wall"
fi

dnl ---------------------------------------------------------------------------
dnl Libraries we need for libknc
dnl ---------------------------------------------------------------------------
LIBKNC_CFLAGS=
LIBKNC_LIBS=
AC_SUBST(LIBKNC_CFLAGS)
AC_SUBST(LIBKNC_LIBS)

AC_PATH_PROG(PKG_CONFIG,pkg-config,"You need pkg-config")
AC_PATH_PROG(GP_PORT_CONFIG,gphoto2-port-config,"You need libgphoto2_port")

dnl ---------------------------------------------------------------------------
dnl Libraries we need for the test program
dnl ---------------------------------------------------------------------------
have_knctest=true
LIBKNCTEST_CFLAGS="`$GP_PORT_CONFIG --cflags`"
LIBKNCTEST_LIBS="`$GP_PORT_CONFIG --libs`"
AM_CONDITIONAL(HAVE_KNCTEST, $have_knctest)
AC_SUBST(LIBKNCTEST_CFLAGS)
AC_SUBST(LIBKNCTEST_LIBS)

dnl ---------------------------------------------------------------------------
dnl Libraries we need for the libgphoto2 plugin
dnl ---------------------------------------------------------------------------
have_gp=false
gp_msg=no
try_gp=true
AC_ARG_WITH(gp, [  --without-libgphto2   Don't compile the libgphoto2 plugin],[
	if test x$withval = xno; then
		try_gp=false
		gp_msg="no (not requested)"
	fi])
if $try_gp; then
	PKG_CHECK_MODULES(GP, libgphoto2, [
		have_gp=true
		GP_PATH="`$PKG_CONFIG --variable=driverdir libgphoto2`"
		gp_msg="yes (${GP_PATH})"])
fi
AM_CONDITIONAL(HAVE_GP, $have_gp)
AC_SUBST(GP_PATH)
AC_SUBST(GP_CFLAGS)
AC_SUBST(GP_LIBS)

dnl ---------------------------------------------------------------------------
dnl libraries we need for the CORBA server
dnl ---------------------------------------------------------------------------
have_corba=false
try_corba=true
corba_msg=no
AC_ARG_WITH(corba,[  --without-corba  Don't compile the corba server],[
	if test x$withval = xno; then
		try_corba=false
		corba_msg="no (not requested)"
	fi])
if $try_corba; then
	PKG_CHECK_MODULES(CORBA, libbonobo-2.0 ORBit-2.0, [
		GNOME_C_IDL="/usr/share/idl/GNOME_C.idl"
		if test -f "${GNOME_C_IDL}"; then
		    ORBIT_IDL="`$PKG_CONFIG --variable=orbit_idl ORBit-2.0`"
		    have_corba=true
		    corba_msg="yes (using ${GNOME_C_IDL})"
		fi
	])
fi
CORBA_CFLAGS="${CORBA_CFLAGS} `$GP_PORT_CONFIG --cflags`"
CORBA_LIBS="${CORBA_LIBS} `$GP_PORT_CONFIG --libs`"
AM_CONDITIONAL(HAVE_CORBA, $have_corba)
AC_SUBST(GNOME_C_IDL)
AC_SUBST(ORBIT_IDL)
AC_SUBST(CORBA_CFLAGS)
AC_SUBST(CORBA_LIBS)

AC_OUTPUT([ intl/Makefile 
Makefile
libknc/Makefile
libknc/libknc.pc
libgphoto2/Makefile
corba/Makefile
tests/Makefile
tests/corba/Makefile
tests/gp/Makefile
])

echo "

Configuration (libknc):

	Source code location:      ${srcdir}
	Version:                   ${VERSION}
	Compiler:                  ${CC}

	libgphoto2 plugin:         ${gp_msg}
	CORBA server:              ${corba_msg}
"
